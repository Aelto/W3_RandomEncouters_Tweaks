
// When the gryphon is fighting with the player. 
// The gryphon is fighting with you until a health threshold. Where he
// will start fleeing
// MULTIPLE state. Can be used multiple times in the encounter
state GryphonFightingPlayer in RandomEncountersReworkedGryphonHuntEntity {
  var can_flee_fight: bool;
  var starting_health: float;

  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    if (previous_state_name == 'FlyingAbovePlayer') {
      this.can_flee_fight = true;
    }
    else {
      this.can_flee_fight = false;
    }

    this.starting_health = parent.this_actor.GetHealthPercents();

    LogChannel('modRandomEncounters', "Gryphon - State GryphonFightingPlayer");

    theSound.SoundEvent("stop_music");
    theSound.SoundEvent("play_music_nomansgrad");
    theSound.SoundEvent("mus_griffin_combat");

    parent.AddTimer('GryphonFightingPlayer_intervalDefaultFunction', 0.5, true);
  }

  timer function GryphonFightingPlayer_intervalDefaultFunction(optional dt : float, optional id : Int32) {
    LogChannel('modRandomEncounters', "health loss: " + (this.starting_health - parent.this_actor.GetHealthPercents()));

    if (this.can_flee_fight && this.starting_health - parent.this_actor.GetHealthPercents() > 0.45) {
      parent.GotoState('GryphonFleeingPlayer');
    }
  }

  event OnLeaveState( nextStateName : name ) {
    parent.RemoveTimer('GryphonFightingPlayer_intervalDefaultFunction');

    theSound.SoundEvent("stop_music");
    theSound.InitializeAreaMusic( theGame.GetCommonMapManager().GetCurrentArea() );


    super.OnLeaveState(nextStateName);
  }
}
